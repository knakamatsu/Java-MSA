plugins {
    id 'org.asciidoctor.convert' version '1.5.3'
    id 'com.github.jruby-gradle.base' version '1.3.3'
}

apply plugin: 'java'
apply plugin: 'org.asciidoctor.convert'

version = '0.2.0'

dependencies {
    gems 'rubygems:asciidoctor-diagram:1.5.4'
}

asciidoctorj {
    version = '1.5.4.1'
}

asciidoctor {
    dependsOn jrubyPrepare
    requires = ['asciidoctor-diagram']
    gemPath = jrubyPrepare.outputDir
    attributes \
        'build-gradle': file('build.gradle'),
        'source-highlighter' : 'coderay',
        'imagesdir': 'images',
        'toc': 'left',
        'icons': 'font',
        'setanchors': '',
        'idprefix': '',
        'idseparator': '-',
        'docinfo1': ''
}


task cleanAll {
  doLast {
    delete '.idea', '.gradle', 'logfile'

    subprojects.each { pj ->
      delete "${pj.name}/build", "${pj.name}/bin", "${pj.name}/logfile"
      delete "${pj.name}/${pj.name}.iml", "${pj.name}/out"
      delete "${pj.name}/.classpath", "${pj.name}/.project", "${pj.name}/.settings/"
    }
  }
}


task publishDocs(type: Sync) {
  from 'build/asciidoc/html5/'
  into 'docs/'
  exclude '.asciidoctor/**'
}
